{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Compute TKE dissipation rate from lidar quasi vertical stares\n",
    "\n",
    "The stabilization table was rolling with the ship, so it sampled some component of the mean horizontal wind.\n",
    "\n",
    "## The ship coordinate system\n",
    "Define the ship coordinate to be \"northward\" toward the bow and \"eastward\" to starboard. The direction a wind is from is 0 from north, and increases to 90° easterly. The $x$ coordinate points forward (north) in this coordinate system, and the $y$ coordinate points to starboard (east). $z$ is upward for a right-handed coordinate system.\n",
    "\n",
    "Pitch $\\theta$ (y-axis) and roll $\\phi$ (x axis) rotations are defined in this coordinate system. Positive roll angle is a rotation of the port upward; positive pitch angle is a rotation of the bow upward.\n",
    "\n",
    "## Winds\n",
    "The vertical wind is $w$. The ship-relative wind \n",
    "is $W$.\n",
    "\n",
    "Ship-relative horizontal wind is\n",
    "$ (U, V) = $ (-speed cos(dir), -speed sin(dir)).\n",
    "\n",
    "Radial velocity away from the lidar is $\\hat{w}$ with\n",
    "$$\n",
    "\\hat{w}^2 = (-U sin\\theta)^2 \n",
    "+ (V cos\\theta sin\\phi)^2 \n",
    "+ (W cos\\theta cos\\phi)^2 \n",
    "$$.\n",
    "\n",
    "## Relative velocity\n",
    "We solve the measured Doppler radial velocity $\\hat w$ for the air velocity,\n",
    "$$\n",
    "W^2 = \\frac{ \\hat{w}^2 \n",
    "- (U sin\\theta)^2 \n",
    "- (V cos\\theta sin\\phi)^2 }\n",
    "{(cos\\theta cos\\phi)^2}\n",
    "$$.\n",
    "\n",
    "## Vectors among the lidar, and two target volumes\n",
    "The radial vector to the target volume is\n",
    "$$\n",
    "{\\bf \\hat{r}} =\n",
    "\\begin{pmatrix}\n",
    "\\hat x \\\\ \\hat y \\\\ \\hat z\n",
    "\\end{pmatrix}\n",
    "=\n",
    "({\\rm range})\n",
    "\\begin{pmatrix}\n",
    "-sin\\theta \\\\\n",
    " cos\\theta sin\\phi \\\\\n",
    " cos\\phi cos\\theta\n",
    "\\end{pmatrix}\n",
    "$$\n",
    "\n",
    "The displacement between two *simultaneous* target\n",
    "volumes is\n",
    "$$\n",
    "\\bf{\\hat r_1 - \\hat r_2} = \n",
    "\\begin{pmatrix}\n",
    "\\hat x \\\\\n",
    "\\hat y \\\\\n",
    "\\hat z\n",
    "\\end{pmatrix}\n",
    "=\n",
    "{\\rm range}\n",
    "\\begin{pmatrix}\n",
    "-sin \\theta_1-(-sin \\theta_2) \\\\\n",
    "cos\\theta_2 sin \\phi_1 - cos\\theta_2 sin \\phi_2 \\\\\n",
    "cos\\theta_1 cos \\phi_1 - cos\\theta_2 cos\\phi_2\n",
    "\\end{pmatrix}\n",
    "$$\n",
    "\n",
    "In the time $\\tau$ the ship moves relative to the air a horizontal displacement of $(X, Y) = \\tau(U,V)$. The total displacement of the sample volumes is \n",
    "$$\n",
    "{\\bf r_2 - r_1} = \n",
    "\\begin{pmatrix}\n",
    "\\hat x + X\\\\\n",
    "\\hat y + Y\\\\\n",
    "\\hat z\n",
    "\\end{pmatrix}\n",
    "$$\n",
    "Expanding, the distance between the sample volumes is \n",
    "$$\n",
    "R = |{\\bf r_2 - r_1}| =\n",
    "\\begin{pmatrix}{|}\n",
    "\\tau U - &{\\rm range}(sin\\theta_1 + sin\\theta_2) \\\\\n",
    "\\tau V + &{\\rm range}(cos\\theta_1 sin\\phi_1 - cos\\theta_2 sin\\phi_2) \\\\\n",
    " & {\\rm range}(cos\\theta_1 cos\\phi_1 - cos\\theta_2 cos\\phi_2)\n",
    "\\end{pmatrix}\n",
    "$$\n",
    "This is the displacement argument of the (second order) structure function \n",
    "$$\n",
    "D(R) = \\overline{(w_2-w_1)^2}\n",
    "$$,\n",
    "which behaves as $D = N + AR^{2/3}$.\n",
    "The structure function is averaged as a function of $R$. We will retain sample pairs of $(D,R)$ and bin average in $R$, perhaps with bins equally populated by sample pairs.\n",
    "\n",
    "## Ship motion fluctuations\n",
    "The ship heave, pitch, and roll also induce fluctuating velocities in the transmitter that must be added. (The separation of scale between the mean ship-relative velocity and these fast fluctuations breaks down in maneuvers where the ship changes direction quickly.) Heave, pitch, and roll is either measured at the lidar on the stabilization table, where it can be added directly to the radial Doppler $\\hat w$, measured in the ship orientation at the location of the lidar, or at a reference location on the ship (see survey).\n",
    "\n",
    "Presently, I read a Notre Dame VectorNav file for measuring and correcting the motion of the table, but I don't know the quantities and units. The ship's POSMV IMU is also available from a reference location. If we use the POSMV, we need to calculate the velocities and moments from the heave and angular rates.\n",
    "\n",
    "Assuming the ship is \n",
    "rigid, the heave is the same everywhere. Angular rates induce motion in proportion to the moment\n",
    "${\\bf L} = (L_x, L_y, L_z)$ \n",
    "be the displacement vector from the reference location \n",
    "(of the POSMV) to the lidar. At the lidar the ship\n",
    "moves by \n",
    "$$\n",
    "\\dot{x} = -\\dot\\theta L_z\\\\\n",
    "\\dot{y} = \\dot\\phi L_z\\\\\n",
    "\\dot{z} = \\dot\\theta L_x - \\dot\\phi L_y\n",
    "$$.\n",
    "Varying yaw is ignored.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(\"/Users/deszoeks/Projects/ASTRAL/lidar\")\n",
    "\n",
    "using Revise\n",
    "using Dates\n",
    "using PyPlot\n",
    "\n",
    "# using CSV\n",
    "# using DataFrames\n",
    "# using DelimitedFiles\n",
    "# using Printf"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "pd = permutedims\n",
    "m2n(x) = ismissing(x) ? NaN : x"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# functions for reading hpl files\n",
    "\n",
    "\"count the headlines before a line that starts with ****\"\n",
    "function numheaderlines(file)\n",
    "    n = 0 \n",
    "    open(file) do f\n",
    "        while !startswith( readline(f), \"****\" )\n",
    "            n += 1\n",
    "        end\n",
    "    end\n",
    "    return n + 1\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "header = read_streamlinexr_head(file_path)\n",
    "Read the Halo Photonics StreamLineXR .hpl file header.\n",
    "\"\"\"\n",
    "function read_streamlinexr_head(file_path)\n",
    "    # put the header in a Dict\n",
    "    h = Dict(\n",
    "        :nlines => countlines(file_path),\n",
    "        :nheaderlines => \n",
    "        # variables defined outside the do block, defaults will be overwritten.\n",
    "        :ngates => Int32(104),\n",
    "        :gatelength => 30.0,\n",
    "        :nrays => Int32(1), # or 40\n",
    "        :start_time => DateTime(2024,5,2,10,0,0),\n",
    "        :vel_resolution => 0.0380,\n",
    "        :headr1 => split(\"time Azimuth Elevation Pitch Roll\"),\n",
    "        :units1 => split(\"hours degrees degrees degrees degrees\"),\n",
    "        :headr2 => [\"Range Gate\", \"Doppler\", \"Intensity\", \"Beta\"],\n",
    "        :units2 => [\"none\", \"(m/s)\", \"(SNR + 1)\", \"(m-1 sr-1)\"]\n",
    "    )\n",
    "\n",
    "    # open and read the file header, updating the header dict a\n",
    "    open(file_path) do file\n",
    "        for _ in 1:2 # skip lines\n",
    "            readline(file)\n",
    "        end\n",
    "\n",
    "        # read number of gates from line 3\n",
    "        h[:ngates] = parse(Int32, last(split(readline(file)))) # 104\n",
    "        # read gate length from line 4\n",
    "        h[:gatelength] = parse(Float64, last(split(readline(file))))\n",
    "        for _ in 5:6 # skip\n",
    "            readline(file)\n",
    "        end\n",
    "\n",
    "        # read number of rays from line 7\n",
    "        h[:nrays] = parse(Int32, last(split(readline(file)))) # 1 for Stare or 40 for User\n",
    "        for _ in 8:9 # skip\n",
    "            readline(file)\n",
    "        end\n",
    "\n",
    "        # read start time from line 10\n",
    "        tl = split(readline(file))\n",
    "        h[:start_time] = DateTime(tl[end-1]*tl[end], \"yyyymmddHH:MM:SS.ss\")\n",
    "        # read velocity resolution (m/s) line 11\n",
    "        h[:vel_resolution] = parse(Float64, last(split(readline(file))))\n",
    "        readline(file) # skip line 12\n",
    "        # read header 1 from line 13\n",
    "        hdrln = split(readline(file))[5:end]\n",
    "        h[:headr1] = hdrln[1:2:end]\n",
    "        h[:units1] = hdrln[2:2:end]\n",
    "        readline(file) # skip line 14\n",
    "        # read header 2 from line 15\n",
    "        hdrln = split(readline(file))[4:end] # but don't parse it\n",
    "        h[:headr2] = [\"Range Gate\", \"Doppler\", \"Intensity\", \"Beta\"]\n",
    "        h[:units2] = [\"none\", \"(m/s)\", \"(SNR + 1)\", \"(m-1 sr-1)\"]\n",
    "        # for _ in 16:17 # skip\n",
    "        #     readline(file)\n",
    "        # end\n",
    "    end\n",
    "\n",
    "    return h\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "modifying read_streamlinexr_data!(file_path, header, beams)\n",
    "Read data and fill in the beams.\n",
    "\"\"\"\n",
    "function read_streamlinexr_stare!(file_path, h, beams, nheaderlines=17)\n",
    "    # use header information in h\n",
    "    nlines = h[:nlines]\n",
    "    ngates = h[:ngates]\n",
    "\n",
    "    # beams could be rays or times\n",
    "    nbeams = round(Int, (nlines-nheaderlines) / (1+ngates)) # = nrays*ntimes\n",
    "    beam_timeangles = zeros(nbeams, 5)\n",
    "    beam_velrad = zeros(nbeams, ngates, 4)\n",
    "\n",
    "    # for User wind profiles beam <--> VAD ray\n",
    "    # for Stare beam <--> time\n",
    "\n",
    "    # open and read the file\n",
    "    open(file_path) do file\n",
    "        for _ in 1:nheaderlines # skip header lines\n",
    "            readline(file)\n",
    "        end\n",
    "\n",
    "        # now read data\n",
    "        for ibeam = 1:nbeams\n",
    "            # beam described by a batch of 1+ngates lines\n",
    "            # Read the beam parameter line\n",
    "            line = readline(file)\n",
    "            beam_timeangles[ibeam,:] .= parse.(Float64, split(line))\n",
    "            # Read each gate in the beam\n",
    "            for igate = 1:ngates\n",
    "                line = readline(file)\n",
    "                beam_velrad[ibeam, igate,:] .= parse.(Float64, split(line))\n",
    "            end\n",
    "        end\n",
    "    end # close the file\n",
    "\n",
    "    # parse the variables into the dict beams\n",
    "    # by beam\n",
    "    beams[:time     ] .= beam_timeangles[:,1] # decimal hours\n",
    "    beams[:azimuth  ] .= beam_timeangles[:,2] # degrees\n",
    "    beams[:elevangle] .= beam_timeangles[:,3] # degrees\n",
    "    beams[:pitch    ] .= beam_timeangles[:,4]\n",
    "    beams[:roll     ] .= beam_timeangles[:,5]\n",
    "    # by gate\n",
    "    beams[:height   ] .= (beam_velrad[1,:,1].+0.5) .* h[:gatelength] # center of gate, assumes same for all beams\n",
    "\n",
    "    # dependent variables (beam, gate)\n",
    "    beams[:dopplervel] .= beam_velrad[:,:,2] # m/s\n",
    "    beams[:intensity ] .= beam_velrad[:,:,3] # SNR + 1\n",
    "    beams[:beta      ] .= beam_velrad[:,:,4] # m-1 sr-1  backscatter?\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "beams, h = read_streamlinexr(file_path)\n",
    "Read the Photonics StreamLineXR .hpl file data and header.\n",
    "\"\"\"\n",
    "function read_streamlinexr_stare(file_path, nheaderlines=17)\n",
    "    # use header information in h\n",
    "    h = read_streamlinexr_head(file_path)\n",
    "    nlines = h[:nlines]\n",
    "    ngates = h[:ngates]\n",
    "\n",
    "    # beams could be rays or times\n",
    "    nbeams = round(Int, (nlines-17) / (1+ngates)) # = nrays*ntimes\n",
    "    # initialize a beams Dict\n",
    "    beams = Dict(\n",
    "        :time      => Vector{Union{Float32,Missing}}(missing, nbeams), # decimal hours\n",
    "        :azimuth   => Vector{Union{Float32,Missing}}(missing, nbeams), # degrees\n",
    "        :elevangle => Vector{Union{Float32,Missing}}(missing, nbeams),\n",
    "        :pitch     => Vector{Union{Float32,Missing}}(missing, nbeams),\n",
    "        :roll      => Vector{Union{Float32,Missing}}(missing, nbeams),\n",
    "\n",
    "        :height    => Vector{Union{Float32,Missing}}(missing, ngates), # center of gate\n",
    "\n",
    "        # dependent variables (beam, gate)\n",
    "        :dopplervel => Matrix{Union{Float32,Missing}}(missing, nbeams,ngates), # m/s\n",
    "        :intensity  => Matrix{Union{Float32,Missing}}(missing, nbeams,ngates), # SNR + 1\n",
    "        :beta       => Matrix{Union{Float32,Missing}}(missing, nbeams,ngates) # m-1 sr-1  backscatter?\n",
    "        )\n",
    "\n",
    "    # read file and fill beams with data\n",
    "    read_streamlinexr_stare!(file_path, h, beams, nheaderlines)\n",
    "    return beams, h\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"plot_stare(beams) plots StreamlineXR lidar backscatter and Doppler velocity.\"\n",
    "function plot_stare(beams)\n",
    "    subplot(2,1,1)\n",
    "    pcolormesh(beams[:time][:], beams[:height][3:end]/1e3, pd(beams[:beta][:,3:end]),\n",
    "        cmap=ColorMap(\"RdYlBu_r\"))\n",
    "    ylim([0, 2])\n",
    "    colorbar()\n",
    "\n",
    "    X = beams[:dopplervel]\n",
    "    X[beams[:intensity] .<= 1.02] .= NaN\n",
    "    subplot(2,1,2)\n",
    "    pcolormesh(beams[:time][:], beams[:height][4:end]/1e3, pd(X[:,4:end]),\n",
    "        cmap=ColorMap(\"RdYlBu_r\"), vmin=-3, vmax=3)\n",
    "    ylim([0, 2])\n",
    "    colorbar()\n",
    "    \n",
    "    tight_layout()\n",
    "    return gcf()\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "lidardir = \"./data/lidar\"\n",
    "datestamp = \"20240508\"\n",
    "datafile = joinpath(lidardir, datestamp, \"Stare_116_$(datestamp)_06.hpl\")\n",
    "\n",
    "beams, head = read_streamlinexr_stare(datafile)\n",
    "beams"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# plot it\n",
    "for datestamp in filter(startswith(\"2024\"), readdir(lidardir))\n",
    "    for starefile in filter(startswith(\"Stare\"), readdir(joinpath(lidardir,datestamp)))\n",
    "        beams, head = read_streamlinexr_stare(joinpath(lidardir,datestamp,starefile))\n",
    "        clf()\n",
    "        plot_stare(beams)\n",
    "        fileout = starefile[1:end-4]*\".png\"\n",
    "        savefig(joinpath(lidardir,\"plot\",fileout))\n",
    "        print(fileout*\", \")\n",
    "    end\n",
    "    print(\"\\n\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# coherent filtering\n",
    "\n",
    "using FFTW\n",
    "using Plots\n",
    "\n",
    "function remove_coherent_component(signal1::Vector{Float64}, signal2::Vector{Float64})\n",
    "    # Fourier Transform of the signals\n",
    "    S1 = fft(signal1)\n",
    "    S2 = fft(signal2)\n",
    "    \n",
    "    # Compute cross-spectral density\n",
    "    P12 = S1 .* conj(S2)\n",
    "    # P21 = conj(P12)\n",
    "    \n",
    "    # Compute auto-spectral density\n",
    "    P11 = S1 .* conj(S1)\n",
    "    P22 = S2 .* conj(S2)\n",
    "    \n",
    "    # Compute coherence\n",
    "    coherence = abs.(P12).^2 ./ (P11 .* P22)\n",
    "    \n",
    "    # Compute the coherent part\n",
    "    coherent_part_S1 = coherence .* S2\n",
    "    coherent_part_S2 = coherence .* S1\n",
    "    \n",
    "    # Remove the coherent part\n",
    "    clean_S1 = S1 .- coherent_part_S1\n",
    "    clean_S2 = S2 .- coherent_part_S2\n",
    "    \n",
    "    # Inverse Fourier Transform to get the time-domain signals\n",
    "    clean_signal1 = real(ifft(clean_S1))\n",
    "    clean_signal2 = real(ifft(clean_S2))\n",
    "    \n",
    "    return clean_signal1, clean_signal2\n",
    "end\n",
    "\n",
    "# Example usage\n",
    "fs = 1000  # Sampling frequency\n",
    "t = 0:1/fs:1-1/fs  # Time array\n",
    "signal1 = sin.(2 * π * 50 * t) .+ 0.5 .* randn(length(t))  # Signal 1 with noise\n",
    "signal2 = sin.(2 * π * 50 * t) .+ 0.5 .* randn(length(t))  # Signal 2 with noise\n",
    "\n",
    "clean_signal1, clean_signal2 = remove_coherent_component(signal1, signal2)\n",
    "\n",
    "# Plot the results\n",
    "plot(t, signal1, label=\"Original Signal 1\")\n",
    "plot!(t, clean_signal1, label=\"Cleaned Signal 1\", linestyle=:dash)\n",
    "\n",
    "plot(t, signal2, label=\"Original Signal 2\")\n",
    "plot!(t, clean_signal2, label=\"Cleaned Signal 2\", linestyle=:dash)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "julia 1.10.3",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
